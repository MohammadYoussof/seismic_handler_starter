! file CC_fk.SHC
!      ======
!
! version 1, 18-SEP-91
!
! crosscorrelation method
! K. Stammler, 18-SEP-91

default 1 ;;           q-file list:
default 2 1            first file:
default 3 %#1(0)       last file:
default 4 -20          window start:
default 5 200          window end:
default 6 ;;           filter:
default 7 ccout        output file prefix:
default 8 2.0          auto-inci time window width:
default 9 0.0          attenuation (t*)
default 10 9.0         corrwdw length

echo ***********************
echo *** using filter #6
echo ***********************
echo *** using attenuation #9
echo ***********************
!echo *** no resampling
!echo ***********************
echo *** no check_sign available
echo ***********************
echo *** no t-component computed
echo ***********************

sdef qfile        ! current q-file
sdef filcnt #2    ! file counter
sdef start
sdef end
sdef recs
sdef rece
sdef inci
sdef begintime $systime

corrl %corrwdw(1) %corrwdw(2)

loop_start:
   if  "filcnt gti #3  goto/forward loop_exit:
   calc s &qfile = %#1("filcnt)
   switch cmderrstop off
   switch noerrmsg on
   calc s &recs = "qfile parse 2
   if  $status eqi 0  goto/forward recno_ok:
      calc i &recs = 1
   recno_ok:
   switch noerrmsg off
   switch cmderrstop on
   calc s &qfile = "qfile parse 1
   calc i &rece = "recs + 2
   echo processing file "qfile ( number "filcnt )
   del all
   dtw
   read "qfile |"recs|-|"rece|
   ! check_sign
   demean all
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #5
   calc r &start = "start + #4
   if  "start ger 0.0  goto/forward start_ok:
      calc r &start = 0.0
   start_ok:
   if  #6 eqs ;;  goto/forward no_filter:
      fili f #6
      filter f 1-3 "start "end
      del 1-3
   no_filter:
   resample all .1
   set all t-origin 0.0
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #8
   autorot 1 2 3 ^azimuth "start "end
   calc r &inci = "g1
   echo inci "inci
   del 1-3
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #10
   unit 1-3 "start "end

   if  #9 eqr 0.0  goto/forward no_attenuation:
      filter a 1;;;#9
      del 1
      display 3 1
   no_attenuation:

   corr/m 1 "start "end 1    ! AC
   corr/m 1 "start "end 2    ! Q-component
   !! corr/m 1 "start "end 3    ! T-component
   set 4 comp l
   set 5 comp q
   !! set 6 comp t

   !! ??? unit 4-6 "start "end
   !! ??? unit 4,5 "start "end
   write |#7|_l| 4
   write |#7|_q| 5
   ! write |#7|_t| 6
   !! set/file 4-6 inci "inci
   set/file 4,5 inci "inci
   calc i &filcnt = "filcnt + 1
goto loop_start:
loop_exit:

echo time: from "begintime to $systime

return
