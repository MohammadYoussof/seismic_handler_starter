! deconvolution with automatic determination of angle of incidence using
! MDIR command
default 1 ;;           q-file list:
default 2 1            first file:
default 3 %#1(0)       last file:
default 4 -20          window start:
default 5 200          window end:
default 6 rest_05_3    filter:
default 7 dcvout_      output file prefix:
default 8 2.0          auto-inci time window width:
default 9 20.0         P-window length (sec)

sdef qfile        ! current q-file
sdef filcnt #2    ! file counter
sdef start
sdef end
sdef fillth       ! length of filter
sdef recs
sdef rece

fct qdir h h

loop_start:
   if  "filcnt gti #3  goto/forward loop_exit:
   calc s &qfile = %#1("filcnt)
   switch cmderrstop off
   calc s &recs = "qfile parse 2
   if  $status eqi 0  goto/forward recno_ok:
      calc i &recs = 1
   recno_ok:
   switch cmderrstop on
   calc s &qfile = "qfile parse 1
   calc i &rece = "recs + 2
   echo processing file "qfile ( number "filcnt )
   del all
   dtw
   read "qfile |"recs|-|"rece|
   remav all
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #5
   calc r &start = "start + #4
   if  "start ger 0.0  goto/forward start_ok:
      calc r &start = 0.0
   start_ok:
   if  #6 eqs ;;  goto/forward no_filter:
      fili f #6
      filter f 1-3 "start "end
      del 1-3
   no_filter:
!   calc s &qfile = ^station qname ^start     ! @@@ tmp
!   fct qdir h
!   write |d31:[b311stamm.qf]|"qfile|d| all   ! @@@ tmp
   resample all .1
   set all t-origin 0.0
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #8
   autorot 1 2 3 ^azimuth "start "end
   echo inci "g1
   del 1-3
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #9
   mark 1 "start
   mark 1 "end
   calc r &fillth = "end - "start
   calc r &fillth = "fillth div 2
   inverse 1 "start "end "fillth ^dcvreg
   unit 1-3 "start "end
   fold 1-3 ;;; 4
   unit 5-7 "start "end
   write |#7|_l| 5
   write |#7|_q| 6
   calc i &filcnt = "filcnt + 1
goto loop_start:
loop_exit:

return
