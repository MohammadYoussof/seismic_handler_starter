
! file CMP20HZ80HZ.SHC
!      ===============
!
! version 3, 26-Feb-98
!
! Compares 20Hz and 80Hz data
! K. Stammler, 30-Dec-97

default 1 bfo            station
default 2 z              component
default 3 3-jan-97_8:37  time
default 4 3600           length
default 5 d2:            20hz data path
default 6 d8:            80hz data path

sdef dev                 ! deviation

switch chatty off

! clear old data and read new
del all
dtw
reads #5 #3 #4 #1 #2 bh
reads #6 #3 #4 #1 #2 hh

if  $dsptrcs eqi 2  goto/forward read_ok:
if  $dsptrcs eqi 1  goto/forward only_1:
   calc r &ret = 0.0 /fmt=<%3.0@f>
   echo "ret
   return
only_1:
   calc r &ret = 999 /fmt=<%3.0@f>
   echo "ret
   return
read_ok:

!! for testing!
!sdef tmp
!calc t &tmp = #3 tadd -0.83
!reads #6 "tmp #4 #1 #2 hh

shift all time_al
! resample 20hz to 80hz and subtract from 80hz
resample 1 ^delta(2)
set 1 weight -1
sum/nonorm 1,2

! compute spectrum of original 80hz and difference; delete other traces
fft 2,3 ;;; 1
del 1-3
cut all 0.1 8
int all
calc r &dev = ^maxval(4) div ^maxval(3)
!calc r &ret = "dev * 100.0 /fmt=<%7.2@f>
calc r &ret = "dev * 100.0 /fmt=<%3.0@f>
echo "ret

return
