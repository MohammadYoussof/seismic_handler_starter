
! file VESPA.SHC
!      =========
!
! version 1, 30-JUL-91
!
! computes vespagram with all traces on display.
! time versus slowness display.
! K. Stammler, 30-JUL-91

default 1 3.0      from slowness
default 2 7.0      to slowness
default 3 0.5      slowness step size
default 4 0.0      azimuth
default 5 1.0      power

sdef slo #1              ! current slowness
sdef trcno $dsptrcs      ! current number of traces on display
sdef vtrcl |1-|$dsptrcs| ! number of input traces
sdef slonum              ! slowness output string
sdef invexp              ! inverse exponent

nr
switch chatty off
copy all
hide _created
if  #5 eqr 1.0  goto/forward no_exp:
   if  #5 eqr 0.0  return
   calc r &invexp = 1.0 div #5
   trcfct "vtrcl spower "invexp
no_exp:

loop_start:
   if  "slo gtr #2  goto/forward loop_exit:
   calc r &slonum = "slo/fmt=<%4.1@f>
   echo/no_lf computing slowness "slonum
   set "vtrcl t-origin 0.0
   shift "vtrcl time_al
   beam "vtrcl #4 "slo
   sum "vtrcl
   calc i &trcno = "trcno + 1
   set "trcno comment |S:|"slonum|
   set "trcno start ^start         ! for plot routine  18-JUL-91
   set "trcno station ^station     ! for plot routine  18-JUL-91
   calc r &slo = "slo + #3
goto loop_start:
loop_exit:

del "vtrcl
if  #5 eqr 1.0  goto/forward no_exp2:
   trcfct all spower #5
no_exp2:
rd
echo $blank

return
