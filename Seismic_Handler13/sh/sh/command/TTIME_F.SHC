! file TTIMES.SHC
!      ==========
!
! version 1, 4-JUL-91
!
! computes and marks travel times of various phases

default 1 1        trace
switch capcnv off
DEFAULT 2 P        phase
SWITCH CAPCNV ON

sdef outfile sys$login:phasefile_tmp   ! scratch file
sdef cmdfile sys$login:phasefile_cmd   ! command file
sdef linecnt 10                        ! line counter
sdef line                              ! current line
sdef phase xxx                         ! current phase
sdef ttime                             ! travel time
sdef tpos                              ! time position

echo phase #2 depth ^depth(#1) distance ^distance(#1)

! create command file
echo_ch/new "cmdfile
echo %dollar |def|%slash|user| sys$output |"outfile|.stx|
echo %dollar ttimes
echo #2
echo $blank
echo ^depth(#1)
echo ^distance(#1)
echo -1
echo -1
echo_ch

! run command file
system |@|"cmdfile|.stx|

line_loop_start:
   switch noerrmsg on
   switch cmderrstop off
   calc s &line = %"outfile("linecnt)
   if  $status nei 0  goto/forward illegal_phase:
   calc i &tpos = 4
   calc s &phase = "line parse 3
   if  "phase eqs #2  goto/forward line_loop_exit:
   calc i &tpos = 3
   calc s &phase = "line parse 2
   if  "phase eqs #2  goto/forward line_loop_exit:
   switch cmderrstop on
   switch noerrmsg off
   calc i &linecnt = "linecnt + 1
goto line_loop_start:
line_loop_exit:
switch cmderrstop on
switch noerrmsg off

calc s &ttime = "line parse "tpos
echo "phase "ttime
calc t &ttime = ^origin(#1) tadd "ttime
echo abs: "ttime
calc t &ttime = "ttime tdiff ^start
echo rel: "ttime

mark/label=#2 #1 "ttime

return

illegal_phase:
echo *** illegal phase #2 ***
return
