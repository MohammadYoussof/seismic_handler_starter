! file AL_READ_S.SHC
!      =============
!
! version 1, 25-Mar-2001
!
! Reads S signal
! K. Stammler, 25-Mar-2001

default 1 ;;   onset time
default 2 sfd: input path
default 3 0.0  back azimuth

sdef rtime      ! read time
sdef ncnt 1     ! N counter
sdef ecnt 2     ! E counter
sdef trcno      ! number of traces

echo reading S signal (T component)

calc t &rtime = #1 tadd -240

reads #2 "rtime 480 grsn ne bh
demean all

nr

! check for completeness of N,E components on all stations
check_loop:
   if  "ecnt gti $dsptrcs  goto/forward check_loop_exit:
   if  ^station("ncnt) eqs ^station("ecnt)  goto/forward check_ok:
      echo deleting "ncnt ^station("ncnt) ^comp("ncnt)
      del "ncnt
      goto check_loop:
   check_ok:
   calc i &ncnt = "ncnt + 2
   calc i &ecnt = "ecnt + 2
goto check_loop:
check_loop_exit:
if  "ncnt gti $dsptrcs  goto/forward nok:
   ! delete last trace since its a single trace
   del $dsptrcs
nok:

! rotate N,E to R,T
calc i &trcno = $dsptrcs
calc i &ncnt = 1
calc i &ecnt = 2
rot_loop:
   if  "ecnt gti "trcno  goto/forward rot_loop_exit:
   rotaz |"ncnt|,|"ecnt| #3
   calc i &ncnt = "ncnt + 2
   calc i &ecnt = "ecnt + 2
goto rot_loop:
rot_loop_exit:

del |1-|"trcno|
del _comp~(t)

rd

return
