! file READ_AND_FK.SHC
!      ===============
!
! version 1, 9-Sep-94
!
! reads in data and performs fk
! parameters are passed via a file, because of there are many
! file consists of:
!   line 01: input directory
!   line 02: start time for fk
!   line 03: time offset in sec for filtering
!   line 04: length of window in sec
!   line 05: station list
!   line 06: component list
!   line 07: channel code
!   line 08: filter
!   line 09: fk lo frq bound
!   line 10: fk hi frq bound
!   line 11: fk max slowness
!   line 12: fk resolution
!   line 13: fk verbosity
!   line 14: fk unit (deg=1, km=0)
! K. Stammler, 9-Sep-94

default 1 ;;      parameter file
default 2 ;;      fk time
default 3 ;;      station list

sdef fk_time #2   ! start time for fk
sdef statlist #3  ! station list
sdef read_time    ! read time
sdef time_offset  ! time offset in sec
sdef read_length  ! read length in sec
sdef trcno        ! number of traces

switch chatty off

if  "fk_time nes ;;  goto/forward fk_time_ok:
	calc s &fk_time = %#1(2)
fk_time_ok:
if  "statlist nes ;;  goto/forward statlist_ok:
	calc s &statlist = %#1(5)
statlist_ok:

del all_dh
dtw

calc r &time_offset = 0.0 - %#1(3)
calc t &read_time = "fk_time tadd "time_offset
calc r &time_offset = 0.0 - "time_offset
calc r &read_length = %#1(4) + "time_offset
reads %#1(1) "read_time "read_length "statlist %#1(6) %#1(7)
demean all
calc i &trcno = $dsptrcs
fili f %#1(8)
filter f all
del |1-|"trcno|
cut all "time_offset $dsp_xmax
set all t-origin 0.0

fk %#1(9) %#1(10) %#1(11) %#1(12) %#1(13) %#1(14) 

return
