
! file DCV_AIRE.SHC
!      ============
!
! version 1, 4-NOV-91
!
! deconvolution with automatic determination of angle of incidence using
! MDIR command
! peak position determined by envelope
! ------------------------------------
! K. Stammler, 30-sep-91

default 1 ;;           q-file list
default 2 1            first file
default 3 %#1(0)       last file
default 4 -20          window start
default 5 200          window end
default 6 ;;           filter
default 7 dcvout_      output file prefix
default 8 2.0          auto-inci time window width
default 9 1.0          regularisation
default 10 0.          attenuation (t*)

echo *******************************
echo *** constant dcvreg of #9 ***
echo *******************************
echo *** no t-component computed ***
echo *******************************
echo ***
echo *** using filter #6
echo --- set chatty
echo *** no check_sign available ***
switch/global chatter on

sdef qfile        ! current q-file
sdef filcnt #2    ! file counter
sdef start
sdef end
!sdef fillth       ! length of filter
sdef recs
sdef rece
sdef inci
sdef begintime $systime
sdef ppos          ! peak position

echo started at "begintime

loop_start:
   if  "filcnt gti #3  goto/forward loop_exit:
   calc s &qfile = %#1("filcnt)
   switch cmderrstop off
   switch noerrmsg on
   calc s &recs = "qfile parse 2
   if  $status eqi 0  goto/forward recno_ok:
      calc i &recs = 1
   recno_ok:
   switch noerrmsg off
   switch cmderrstop on
   calc s &qfile = "qfile parse 1
   calc i &rece = "recs + 2
   echo processing file "qfile ( number "filcnt )
   del all
   dtw
   read/swap "qfile |"recs|-|"rece|
   ! check_sign
   demean all
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #5
   calc r &start = "start + #4
   if  "start ger 0.0  goto/forward start_ok:
      calc r &start = 0.0
   start_ok:
   if  #6 eqs ;;  goto/forward no_filter:
      fili f #6
      filter f 1-3 "start "end
      del 1-3
   no_filter:
   resample all .1
   set all t-origin 0.0
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + #8
   autorot 1 2 3 ^azimuth "start "end
   calc r &inci = "g1
   echo inci "inci
   del 1-3
   if  #10 eqr 0.0  goto/forward no_attenuation:
      filter a 1;;;#10
      del 1
      display 3 1
   no_attenuation:
   calc t &start = ^p-onset tdiff ^start
   calc r &end = "start + ^pwdw
   mark 1 "start
   mark 1 "end
   unit 1-3 "start "end

!  determine maximum of envelope
   envelope 1
   am 4 "start "end;;;;&ppos
	del 4
   relppos 1 "ppos    ! returns "ret

   spiking/pos="ret 1 "start "end #9
!>> including t-component
!   fold 1-3 ;;; 4
!   unit 5-7 "start "end
!   write |#7|_l| 5
!   write |#7|_q| 6
!   write |#7|_t| 7
!   set/file 5-7 inci "inci
!>> without t-component
   fold 1,2 ;;; 4
   unit 5,6 "start "end
   write |#7|_l| 5
   write |#7|_q| 6
   set/file 5,6 inci "inci
!>> -------------------
   calc i &filcnt = "filcnt + 1
goto loop_start:
loop_exit:

echo terminated $systime
calc t &inci = $systime tdiff "begintime
echo needed "inci seconds

return
