! plots traces from all q-files of a q-file list.  One page per file.
! uses output files of FILE_INFO command procedure
default 1 ;;      q-file list
default 2 1       first file
default 3 %#1(0)  last file
default 4 -20     time window start
default 5 120     time window end
default 6 ;;      filter
default 7 35      minimum distance
default 8 95      maximum distance
default 9 90      minimum excl. depth
default 10 350    maximum excl. depth

sdef qfile     ! name of current qfile
sdef qno       ! trace number of trace in q-file
sdef qno_e     ! last trace
sdef tline     ! title line
sdef filcnt #2 ! file counter
sdef wdws      ! start of time window
sdef wdwe      ! end of time window
sdef trcno     ! number of traces
sdef tmp
sdef plotno 1  ! number of plots

fct axis timeaxis labcnt 5
fct setstyle $titlestyle charsize .025
fct setstyle $timeaxisstyle charsize .02

set/default all reduction 2

loop_start:
   if  "filcnt gti #3  goto/forward loop_exit:
   del all
   dtw
   norm af
   calc s &qfile = %#1("filcnt)
   calc s &qno = "qfile parse 2
   calc s &qfile = "qfile parse 1
   calc i &qno_e = "qno + 2
   nr
   read "qfile |"qno|-|"qno_e|
   switch cmderrstop off
   echo "qfile ^eventno
   if  $status nei 0  goto/forward noplot:
   if  ^distance ltr #7  goto/forward noplot:
   if  ^distance gtr #8  goto/forward noplot:
   if  ^depth ler #9  goto/forward doplot:
   if  ^depth ger #10  goto/forward doplot:
   goto/forward noplot:
   doplot:
      switch cmderrstop on
      calc t &wdws = ^p-onset tdiff ^start
      calc r &wdwe = "wdws + #5
      calc r &wdws = "wdws + #4
      stw "wdws "wdwe
      calc s &tline = |^start|$blank|"qfile|$blank|"qno|
      title 1 "tline
      calc r &tmp = ^distance/fmt=<%5.1@f>
      calc s &tline = |"plotno|:|$blank|dst:|"tmp|
      calc r &tmp = ^azimuth/fmt=<%5.1@f>
      calc s &tline = |"tline|$blank|az:|"tmp|
      calc r &tmp = ^depth/fmt=<%5.1@f>
      calc s &tline = |"tline|$blank|dp:|"tmp|$blank|ev:|^eventno|
      calc s &tline = |"tline|$blank|f:|#6|
      title 2 "tline
      calc i &plotno = "plotno + 1
      if  #6 eqs ;;  goto/forward nofilter:
         calc i &trcno = $tottrcs
         remav all
         fili f #6
         calc r &wdwe = "wdwe + 30.0
         filter f all
         del |1-|"trcno|
      nofilter:
      norm aw
      rd r
      hc
   noplot:
   switch cmderrstop on
   calc i &filcnt = "filcnt + 1
goto loop_start:
loop_exit:

return
