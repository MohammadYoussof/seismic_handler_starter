
! file SHM_CMD_VESPA.SHC
!      =================
!
! version 1, 19-Aug-94
!
! computes vespagram with all traces on display.
! time versus slowness display.
! K. Stammler, 19-Aug-94

default 1 3.0      from slowness
default 2 7.0      to slowness
default 3 0.5      slowness step size
default 4 0.0      azimuth
default 5 1.0      power

sdef slo #1              ! current slowness
sdef trcno $dsptrcs      ! current number of traces on display
sdef vtrcl |1-|$dsptrcs| ! number of input traces
sdef slonum              ! slowness output string
sdef invexp              ! inverse exponent
sdef zoom                ! zoom factor
sdef cnt                 ! counter
sdef refstat mox        ! reference station

nr

!calc i &cnt = 1
!floop_start:
!   if  "cnt gti $dsptrcs  goto/forward floop_exit:
!   if  ^station("cnt) nes gra1  goto/forward cont:
!      calc s &refstat = gra1
!      goto/forward floop_exit:
!   cont:
!   calc i &cnt = "cnt + 1
!goto floop_start:
!floop_exit:

switch chatty off
calc r &zoom = ^zoom
copy all
hide |1-|"trcno|
if  #5 eqr 1.0  goto/forward no_exp:
   if  #5 eqr 0.0  return
   calc r &invexp = 1.0 div #5
   trcfct "vtrcl spower "invexp
no_exp:

loop_start:
   if  "slo gtr #2  goto/forward loop_exit:
   calc r &slonum = "slo/fmt=<%4.1@f>
   echo/no_lf computing slowness "slonum
   set "vtrcl t-origin 0.0
   shift "vtrcl time_al
   beam "vtrcl #4 "slo
   sum "vtrcl
   calc i &trcno = "trcno + 1
   set "trcno station |S:|"slonum|
   calc r &slo = "slo + #3
goto loop_start:
loop_exit:

del "vtrcl
if  #5 eqr 1.0  goto/forward no_exp2:
   trcfct all spower #5
no_exp2:
zoom all "zoom
rd
echo $blank

!set 1 station "refstat

return
