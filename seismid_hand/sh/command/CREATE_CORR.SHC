! file CREATE_CORR.SHC
! computes crosscorrelation from three input traces Z,N,E at positions 1,2,3

default 1 20.0      correlation window
default 2 ;;        attenuation
default 3 2         correlation trace

sdef lobound      ! lower time bound
sdef hibound      ! upper time bound
sdef incidence    ! angle of incidence
sdef tmp          ! scratch

! rotate traces to L,Q,T
calc r &tmp = 0 - ^azimuth    ! take negative azimuth for 2-dim rotation
rot 2,3 "tmp
calc t &lobound = ^p-onset tdiff ^start   ! compute time window
calc r &hibound = "lobound + #1
mdir 1,4 "lobound "hibound &incidence     ! get angle of incidence
calc r &incidence = 0 - "incidence        ! invert sign
if  "incidence ger 0.0  goto/forward no_msg:
   echo file ^file rec ^recno inci "incidence distance ^distance
no_msg:
del _created                              ! delete result of 2-dim rotation
rot 1-3 ^azimuth "incidence               ! rotate 3-dim
del 1-3                                   ! delete original traces

! attenuate if requested
if  #2 eqs ;;  goto/forward no_att:
   filter att 1;;;#2
   del 1
   display 3 1
no_att:

! do crosscorrelation
corr/m 1 "lobound "hibound #3
del 1-3
calc r &g1 = "incidence

return
