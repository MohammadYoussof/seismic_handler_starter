! file ident.shc
!      ============

default 1 1    trace number
SWITCH CAPCNV OFF
DEFAULT 2 P    phase
SWITCH CAPCNV ON

sdef ptime
sdef cmd
sdef max
sdef cnt 1
sdef origin       ! origin time
sdef dist         ! distance
sdef depth        ! depth of event

echo mark phase #2
time &ptime
calc s &cmd = |findev|$blank|^station(#1)|$blank|$quotes|#2|$quotes|$blank|"ptime|
switch capcnv off
CALC S &CMD = |"CMD|$BLANK|$QUOTES|-o=XPID.STX|$QUOTES|
IF  #TOL EQS _EXISTSNOT_  GOTO/FORWARD NO_TOL:
   CALC S &CMD = |"CMD|$BLANK|$QUOTES|-t=|#TOL|$QUOTES|
NO_TOL:
SWITCH CAPCNV ON
echo "cmd
system "cmd

calc s &g1 = %xpid
calc i &max = %xpid(0)
loop_start:
   if  "cnt gti "max  goto/forward loop_exit:
   echo %xpid("cnt)
   calc i &cnt = "cnt + 1
goto loop_start:
loop_exit:

if  ^fromq(#1) eqs N  return
if  #nofile nes _existsnot_  return
if  #file nes _existsnot_  goto/forward insert:
enter &cmd insert to q-file ? [y|n]:
if  "cmd nes Y  return
insert:

calc s &origin = "g1 parse 2
calc s &dist = "g1 parse 7
calc s &depth = "g1 parse 5
echo insert info values to q-file for trace number #1

if  ^modif(#1) eqs y  goto/forward  modified_traces:
set/file #1 origin "origin
set/file #1 distance "dist
set/file #1 depth "depth
return

modified_traces:
set/file/mod #1 origin "origin
set/file/mod #1 distance "dist
set/file/mod #1 depth "depth
return
